**CAOS2PRAY
*# Pray-File "puffyPlant.agents"
*# DS-Name "Puffy Plant"
*# Agent Animation File = "puff-plant.c16"
*# Agent Sprite First Image = 40
*# desc = "A plant which is puffy."

*# attach puff-plant.c16

new: simp 2 3 24404 "puff-plant" 6 57 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 1
accg 0.2
aero 10
fric 95
tick 4

setv va42 1
doif va42 = 0
* actual injection location
*	in comms room
	setv va00 game "CreatorY"
	mvto game "CreatorX" va00
elif va42 = 1
* testing injection location
*	near hand. Random x offset, so help scatter seeds
	setv va00 mopy
	subv va00 hght
	setv va01 mopx
	addv va01 rand -200 200
	mvsf va01 va00
endi

scrp 2 3 24404 9
	doif pose = 0
*	we have a feather!
********************
***timer for wind***
********************
		doif fall = 1
*	we're falling. Blow with the wind!
*		don't jitter too much
			doif rand 0 1 = 0
*			remember our x velocity
				setv va10 velx
*			pick an amount for the wind to blow (defaults to "blow to the right")
				setv va11 rand .1 1
*		The wind doesn't change directions constantly.
*		So probably keep blowing in the same direction
				doif va10 = 0
*			we're not blowing in any direction yet, so pick one
					doif rand 0 1 = 0
						mulv va11 -1
					endi
				elif va10 < 0
*			we're blowing to the left, so keep blowing to the left
					mulv va11 -1
*				else
*			we're blowing to the right, so stay with the default, "blow to the right"
				endi
*		But the wind might change
				doif rand 0 3 = 0
					mulv va11 -1
				endi
*			set the new velocity
				addv va10 va11
				velo va10 vely
			endi
			stop
		else
*		we've landed!
			wait 100
			doif carr = null and fall = 0
				lock
*			we're not being carried and we're not falling
*		lose the feather
				frat rand 10 14
				anim [1 2 3 4 5]
				over
				aero 0
*	we still need to tick (for growing and stuff) but not nearly as fast
*				1 seconds (probably more like 2-5(?, hmm) min in the actual agent
				tick 20
			endi
		endi
		stop
	else
		wait rand 0 100
*	we have no feather
********************
***timer for growing***
********************
		doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
			tick 20
			stop
		endi
*	check room type is soil
		doif rtyp room ownr < 5 or rtyp room ownr > 7
*dbg: outs "<<puff plant seed>> not soil"
			gsub rot
		endi
*	check room properties
*		Get Local Room ID
		targ ownr
		setv va00 grap posx posy
*		really dark?
		doif prop va00 1 < .05
*dbg: outs "<<puff plant seed>> too dark"
			gsub rot
		endi
*		too chilly?
		doif prop va00 2 < .4
*dbg: outs "<<puff plant seed>> too cold"
			gsub rot
		endi
*		really really bone dry or a bit too wet?
		doif prop va00 3 < .00001 or prop va00 3 > .3
*dbg: outs "<<puff plant seed>> really dry or too wet"

			gsub rot
		endi
*		too few nutrients?
		doif prop va00 4 < .1
*dbg: outs "<<puff plant seed>> too few nutrients"
			gsub rot
		endi
		inst
		setv va40 0
*	check population for 1 within 100 pixels
		rnge 100
		esee 2 4 24404
			addv va40 1
		next
		doif va40 > 1
			gsub rot
		endi
*	check population for 5 within 1000 pixels
		rnge 1000
		esee 2 4 24404
			addv va40 1
		next
		doif va40 > 5
			gsub rot
		endi
		etch 2 4 24404
			gsub rot
		next
*	calculate location of new plant agent
		setv va00 posl
		subv va00 30
		setv va01 post
		subv va01 0
		new: comp 2 4 24404 "puff-plant" 50 0 292
		setv name "height" rand 1 5
*		current height
		setv ov10 0
*		current part number
		setv ov11 0
*		set state "no flower"
		setv ov12 0
		tick rand 35 45
		mvto va00 va01
		kill ownr
		slow

		subr rot
			kill ownr
		retn
	endi
endm

scrp 2 4 24404 9
	doif ov12 = 0
*	state "no flower"
		doif ov11 = 0
*	no part 1,
			subv ov10 31
			addv ov11 1
			pat: dull ov11 "puff-plant" 8 0 ov10 -1
		elif ov11 = 1
*	grow regular segment with random height
			subv ov10 rand 0 31
			addv ov11 1
			pat: dull ov11 "puff-plant" rand 9 19 0 ov10 0
		elif ov11 <= name "height"
*	grow regular segment
			subv ov10 31
			addv ov11 1
			pat: dull ov11 "puff-plant" rand 9 19 0 ov10 0
		else
*	grow flower
			subv ov10 61
			addv ov11 1
			pat: dull ov11 "puff-plant" 40 0 ov10 1
			part ov11
			setv va10 0
			reps 4
				wait 40
				addv va10 1
				pose va10
			repe
*	flower grown
			setv va20 posx
			setv va21 posy
			addv va21 ov10
			subv va20 3
			addv va21 10
			setv va24 plne
			addv va24 100
			wait 40
			inst
*		check if we can explode here
			new: simp 2 3 24404 "puff-plant" 6 57 va24
			perm 100
			doif tmvt va20 va21 = 0
				kill targ
				kill ownr
				stop
			else
				kill targ
			endi
*			loop
			inst
			reps rand 50 150
				new: simp 2 3 24404 "puff-plant" 6 57 va24
				attr 211
				perm 64
				elas 1
				accg 0.2
				aero 10
				fric 95
				tick 4
				mvto va20 va21
*			Explode in a circle
*				Random direction
				setv va25 rand 0 360
*				Translate to X and Y
				setv va26 sin_ va25
				setv va27 cos_ va25
*				Two "rings" of velocity. So the circle can be full (maybe refactor out the "rand" at some point)
				doif rand 0 1 = 0
					mulv va26 8
					mulv va27 8
				else
					mulv va26 4
					mulv va27 4
				endi
*				Make the "rings" fuzzy. So they turn into a circle
				addv va26 rand -2 2
				addv va27 rand -4 0
				velo va26 va27
			repe
*				wait 20
*			ever										
			wait 3
			targ ownr
			part ov11
			pose 5
			wait rand 0 1000
			kill ownr
		endi
	else
*	state "has flower"

	endi
endm

rscr
enum 2 3 24404
	kill targ
next
enum 2 4 24404
	kill targ
next

scrx 2 3 24404 9
scrx 2 4 24404 9

endm