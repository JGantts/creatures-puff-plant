**CAOS2PRAY
*# Pray-File "puffyPlant.agents"
*# DS-Name "Puffy Plant"
*# Agent Animation File = "puff-plant.c16"
*# Agent Sprite First Image = 3
*# desc = "A plant which is puffy."

*# attach bendyPlant.c16 bendyPlant.catalogue

new: simp 2 3 24404 "puff-plant" 2 50 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 1
accg 0.2
aero 10
fric 95
tick 1

setv va00 game "CreatorY"
*setv va00 mopy
subv va00 hght
mvto game "CreatorX" va00
*mvto mopx va00


scrp 2 3 24404 9
	doif pose = 0
*	we have a feather!
********************
***timer for wind***
********************
		doif fall = 1
*	we're falling. Blow with the wind!
*		don't jitter too much
			doif rand 0 2 = 0
*			remember our x velocity
				setv va10 velx
*			pick an amount for the wind to blow (defaults to "blow to the right")
				setv va11 rand .1 1
*		The wind doesn't change directions constantly.
*		So probably keep blowing in the same direction
				doif va10 = 0
*			we're not blowing in any direction yet, so pick one
					doif rand 0 1 = 0
						mulv va11 -1
					endi
				elif va10 < 0
*			we're blowing to the left, so keep blowing to the left
					mulv va11 -1
				endi
*			we'r blowing to the right, so stay with the default, "blow to the right"
*		But the wind might change
				doif rand 0 3 = 0
					mulv va11 -1
				endi
*			set the new velocity
				addv va10 va11
				velo va10 vely
			endi
			stop
		else
*		we've landed!
			wait 100
			doif carr = null and fall = 0
*			we're not being carried and we're not falling
*		lose the feather (maybe do something else? for now we can test the next falling stage)
				pose 1
				aero 0
*	we still need to tick (for growing and stuff) but not nearly as fast
*				1 seconds (probably more like 2-5(?, hmm) min in the actual agent
				tick 20
			endi
		endi
		stop
	else
*	we have no feather
********************
***timer for growing***
********************
		doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
			tick 20
			stop
		endi
		doif rtyp room ownr >= 5 and rtyp room ownr <= 7
* room type is soil
			inst
* calculate location of new plant agent
			setv va00 posl
			subv va00 30
			setv va01 post
			subv va01 0
			new: comp 2 4 24404 "puff-plant" 50 0 292
*			current height
			setv ov10 0
			tick 20
			mvto va00 va01
			kill ownr
			slow
		else
* room type is not soil
		endi
	endi
endm

scrp 2 4 24404 9
	doif pnxt 0 = -1
*	no part 1,
		subv ov10 31
		pat: dull 1 "puff-plant" 4 0 ov10 -1
	elif pnxt 1 = -1
*	no part 2
		subv ov10 rand 0 31
		pat: dull 2 "puff-plant" rand 5 15 0 ov10 -1
	elif pnxt 2 = -1
*	no part 3
		subv ov10 31
		pat: dull 3 "puff-plant" rand 5 15 0 ov10 -1
	elif pnxt 3 = -1
		subv ov10 31
		pat: dull 4 "puff-plant" rand 5 15 0 ov10 -1
	elif pnxt 4 = -1
		subv ov10 31
		pat: dull 5 "puff-plant" rand 5 15 0 ov10 -1
	elif pnxt 5 = -1
		subv ov10 61
		pat: dull 6 "puff-plant" 36 0 ov10 -2
		part 6
		setv va10 0
		reps 4
			dbg: outv pose
			addv va10 1
			pose va10
			wait 40
		repe
	endi
endm

rscr
enum 2 3 24404
	kill targ
next
enum 2 4 24404
	kill targ
next

scrx 2 3 24404 9
scrx 2 4 24404 9

endm