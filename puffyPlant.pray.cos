**CAOS2PRAY
*# Pray-File "puffyPlant.agents"
*# DS-Name "Puffy Plant"
*# Agent Animation File = "puff-plant.c16"
*# Agent Sprite First Image = 40
*# desc = "A plant which is puffy."

*# attach puff-plant.c16

new: simp 2 3 24404 "puff-plant" 6 57 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 1
accg 0.2
aero 10
fric 95
tick 4

setv va42 1
doif va42 = 0
* actual injection location
*	in comms room
	setv va00 game "CreatorY"
	mvto game "CreatorX" va00
elif va42 = 1
* testing injection location
*	near hand. Random x offset, so help scatter seeds
	setv va00 mopy
	subv va00 hght
	setv va01 mopx
	addv va01 rand -200 200
	mvsf va01 va00
endi

scrp 2 3 24404 9
	doif pose = 0
*	we have a feather!
********************
***timer for wind***
********************
		doif fall = 1
*	we're falling. Blow with the wind!
*		don't jitter too much
			doif rand 0 1 = 0
*			remember our x velocity
				setv va10 velx
*			pick an amount for the wind to blow (defaults to "blow to the right")
				setv va11 rand .1 1
*		The wind doesn't change directions constantly.
*		So probably keep blowing in the same direction
				doif va10 = 0
*			we're not blowing in any direction yet, so pick one
					doif rand 0 1 = 0
						mulv va11 -1
					endi
				elif va10 < 0
*			we're blowing to the left, so keep blowing to the left
					mulv va11 -1
*				else
*			we're blowing to the right, so stay with the default, "blow to the right"
				endi
*		But the wind might change
				doif rand 0 3 = 0
					mulv va11 -1
				endi
*			set the new velocity
				addv va10 va11
				velo va10 vely
			endi
			stop
		else
*		we've landed!
			wait 100
			doif carr = null and fall = 0
				lock
*			we're not being carried and we're not falling
*		lose the feather
				frat rand 10 14
				anim [1 2 3 4 5]
				over
				aero 0
*	we still need to tick (for growing and stuff) but not nearly as fast
*				1 seconds (probably more like 2-5(?, hmm) min in the actual agent
				tick 20
			endi
		endi
		stop
	else
		wait rand 0 100
*	we have no feather
********************
***timer for growing***
********************
		doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
			tick 20
			stop
		endi
*	check room type is soil
		doif rtyp room ownr < 5 or rtyp room ownr > 7
*dbg: outs "<<puff plant seed>> not soil"
			gsub rot
		endi
*	check room properties
*		Get Local Room ID
		targ ownr
		setv va00 grap posx posy
*		really dark?
		doif prop va00 1 < .05
*dbg: outs "<<puff plant seed>> too dark"
			gsub rot
		endi
*		too chilly?
		doif prop va00 2 < .4
*dbg: outs "<<puff plant seed>> too cold"
			gsub rot
		endi
*		really really bone dry or a bit too wet?
		doif prop va00 3 < .00001 or prop va00 3 > .3
*dbg: outs "<<puff plant seed>> really dry or too wet"
			gsub rot
		endi
*		too few nutrients?
		doif prop va00 4 < .1
*dbg: outs "<<puff plant seed>> too few nutrients"
			gsub rot
		endi
		inst
		setv va40 0
*	check population touching
		etch 2 4 24404
			gsub rot
*dbg: outs "<<puff plant seed>> stop touching me!"
		next
*	check population for 1 within 100 pixels
		rnge 100
		esee 2 4 24404
			addv va40 1
		next
		doif va40 > 1
*dbg: outs "<<puff plant seed>> too crowded, 100"
			gsub rot
		endi
*	check population for 5 within 1000 pixels
		rnge 1000
		esee 2 4 24404
			addv va40 1
		next
		doif va40 > 5
*dbg: outs "<<puff plant seed>> too crowded, 1000"
			gsub rot
		endi
*	calculate location of new plant agent
		setv va00 posl
		subv va00 30
		setv va01 post
		subv va01 0
		new: comp 2 4 24404 "puff-plant" 50 0 292
		setv name "height" rand 0 1
*		current height
		setv name "CurrentHeight" 0
*		current part number
		setv name "CurrentPart" 0
*		set state "no flower"
		setv name "Flower" 0
		tick rand 35 45
		mvto va00 va01
		kill ownr
		slow

		subr rot
*dbg: outs "<<puff plant seed>> rotting"
			kill ownr
		retn
	endi
endm

scrp 2 4 24404 9
	doif mame "Flower" = 0
*	state "no flower"
		doif name "CurrentPart" = 0
*	no part 1,
			subv name "CurrentHeight" 31
			addv name "CurrentPart" 1
			pat: dull name "CurrentPart" "puff-plant" 8 0 name "CurrentHeight" -1
		elif name "CurrentPart" = 1
*	grow regular segment with random height
			subv name "CurrentHeight" rand 0 31
			addv name "CurrentPart" 1
			pat: dull name "CurrentPart" "puff-plant" rand 9 19 0 name "CurrentHeight" 0
		elif name "CurrentPart" <= name "height"
*	grow regular segment
			subv name "CurrentHeight" 31
			addv name "CurrentPart" 1
			pat: dull name "CurrentPart" "puff-plant" rand 9 19 0 name "CurrentHeight" 0
		else
*	grow flower base
			subv name "CurrentHeight" 61
			addv name "CurrentPart" 1
			pat: dull name "CurrentPart" "puff-plant" 40 0 name "CurrentHeight" 1
			part name "CurrentPart"
			frat 11
			anim [1 2 3 4]
			over
			tick 1000
			inst
			setv name "Flower" 1
*		make the flower toy agent
*			remember plane
			setv va00 plne
			addv va00 3
*			remember position
			setv va01 posl
			setv va02 post
*			nudge it into place
			addv va02 name "CurrentHeight"
			addv va01 10
			addv va02 3
			new: simp 2 21 24404 "puff-plant" 8 49 295
			setv name "Free" 0
			attr 4
			bhvr 43
			mvto va01 va02
			seta name "Stalk" ownr
			seta mame "Ball" targ
			targ ownr
			part mame "CurrentPart"
			pose 5
		endi
	elif mame "Flower" = 1
*	state "has flower"
		inst
		doif mame "Ball" <> null
			mesg writ mame "Ball" 1000
		endi
		setv mame "Flower" 2
		slow
		tick rand 20 1000
	elif mame "Flower" = 2
*	state "grew flower and it's gone"
		kill ownr
	endi
endm

*Stalk - LostBall
scrp 2 4 24404 1000

endm

*Ball - Explode
scrp 2 21 24404 1000
	setv va20 posx
	setv va21 posy
	subv va21 6
	setv va24 plne
	addv va24 100
	inst
*	check if we can explode here
	new: simp 2 3 24404 "puff-plant" 6 57 va24
	perm 100
	doif tmvt va20 va21 = 0
		kill targ
		kill ownr
		stop
	else
		kill targ
	endi
*	random size of exploding ball 
*		(used for both number of seeds and max velocity of seeds)
	setv va28 rand 9 15
	setv va30 va28
	mulv va30 10
	inst
	reps va30
		new: simp 2 3 24404 "puff-plant" 6 57 va24
		attr 211
		perm 64
		elas 1
		accg 0.2
		aero 10
		fric 95
		tick 4
		mvto va20 va21
*	Explode in a circle
*		Random direction
		setv va25 rand 0 360
*		Translate to X and Y
		setv va26 cos_ va25
		setv va27 sin_ va25
*		Random speed
*		(would otherwise make a ring of seeds, instead of a ball)
		setv va29 rand 2 va28
		mulv va26 va29
		mulv va27 va29
*		Set velocity
		velo va26 va27
	repe
*	let seed-ball expand to juuust the right amount
	wait 3
	inst
	doif name "Free" = 0
		mesg writ mame "Stalk" 1000
	endi
	kill ownr
endm

*Ball - Activate 1
scrp 2 21 24404 1
dbg: outs "<<puff plant ball>> activate 1"
	inst
	doif name "Free" = 0
		setv name "Free" 1
*		Add physics and allow hand to carry
		attr 198
		mesg writ mame "Stalk" 1000
	endi
	doif from = pntr
		mesg writ ownr 1000
	endi
endm

*Ball - Activate 2
scrp 2 21 24404 2
dbg: outs "<<puff plant ball>> activate 2"
	lock
	doif name "Free" = 0
		setv name "Free" 1
*		Add physics and allow hand to carry
		attr 198
		mesg writ mame "Stalk" 1000
	endi
endm

*Ball - Hit
scrp 2 21 24404 3
dbg: outs "<<puff plant ball>> hit"
	lock
	doif name "Free" = 0
		setv name "Free" 1
*		Add physics and allow hand to carry
		attr 198
		mesg writ mame "Stalk" 1000
	endi
endm

*Ball - Picked up
scrp 2 21 24404 5
dbg: outs "<<puff plant ball>> picked up"
	inst
	lock
	doif name "Free" = 0
		setv name "Free" 1
*		Add physics and allow hand to carry
		attr 198
		mesg writ mame "Stalk" 1000
	endi
endm

rscr
enum 2 3 24404
	kill targ
next
enum 2 4 24404
	kill targ
next
enum 2 21 24404
	kill targ
next

scrx 2 3 24404 0
scrx 2 4 24404 0
scrx 2 21 24404 0
endm