**CAOS2PRAY
*# Pray-File "puffyPlant.agents"
*# DS-Name "Puffy Plant"
*# Agent Animation File = "puff-plant.c16"
*# Agent Sprite First Image = 40
*# desc = "A plant which is puffy."

*# attach puff-plant.c16

new: simp 2 3 24404 "puff-plant" 2 50 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 1
accg 0.2
aero 10
fric 95
tick 4

setv va00 game "CreatorY"
*setv va00 mopy
subv va00 hght
mvto game "CreatorX" va00
*mvto mopx va00


scrp 2 3 24404 9
	doif pose = 0
*	we have a feather!
********************
***timer for wind***
********************
		doif fall = 1
*	we're falling. Blow with the wind!
*		don't jitter too much
			doif rand 0 1 = 0
*			remember our x velocity
				setv va10 velx
*			pick an amount for the wind to blow (defaults to "blow to the right")
				setv va11 rand .1 1
*		The wind doesn't change directions constantly.
*		So probably keep blowing in the same direction
				doif va10 = 0
*			we're not blowing in any direction yet, so pick one
					doif rand 0 1 = 0
						mulv va11 -1
					endi
				elif va10 < 0
*			we're blowing to the left, so keep blowing to the left
					mulv va11 -1
				endi
*			we'r blowing to the right, so stay with the default, "blow to the right"
*		But the wind might change
				doif rand 0 3 = 0
					mulv va11 -1
				endi
*			set the new velocity
				addv va10 va11
				velo va10 vely
			endi
			stop
		else
*		we've landed!
			wait 100
			doif carr = null and fall = 0
*			we're not being carried and we're not falling
*		lose the feather (maybe do something else? for now we can test the next falling stage)
				pose 1
				aero 0
*	we still need to tick (for growing and stuff) but not nearly as fast
*				1 seconds (probably more like 2-5(?, hmm) min in the actual agent
				tick 20
			endi
		endi
		stop
	else
		wait rand 0 100
*	we have no feather
********************
***timer for growing***
********************
		doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
			tick 20
			stop
		endi
		setv va40 0
		inst
		esee 2 4 24404
			addv va40 1
		next
		etch 2 4 24404
			addv va40 15
		next
		slow
		doif va40 > 4
			kill ownr
		endi
		doif rtyp room ownr >= 5 and rtyp room ownr <= 7
* room type is soil
			inst
* calculate location of new plant agent
			setv va00 posl
			subv va00 30
			setv va01 post
			subv va01 0
			new: comp 2 4 24404 "puff-plant" 50 0 292
*			current height
			setv ov10 0
*			current part number
			setv ov11 0
*			set state "no flower"
			setv ov12 0
			tick rand 35 45
			mvto va00 va01
			kill ownr
			slow
		else
* room type is not soil
		endi
	endi
endm

scrp 2 4 24404 9
	doif ov12 = 0
*	state "no flower"
		doif ov11 = 0
*	no part 1,
			subv ov10 31
			addv ov11 1
			pat: dull ov11 "puff-plant" 4 0 ov10 -1
		elif ov11 = 1
*	grow regular segment with random height
			subv ov10 rand 0 31
			addv ov11 1
			pat: dull ov11 "puff-plant" rand 5 15 0 ov10 0
		elif ov11 <= 4
*	grow regular segment
			subv ov10 31
			addv ov11 1
			pat: dull ov11 "puff-plant" rand 5 15 0 ov10 0
		else
*	glow flower
			subv ov10 61
			addv ov11 1
			pat: dull ov11 "puff-plant" 36 0 ov10 1
			part 6
			setv va10 0
			reps 4
*dbg: outv pose
				addv va10 1
				pose va10
				wait 40
			repe
*	flower grown
			setv va20 posx
			setv va21 posy
			addv va21 ov10
			subv va20 3
			addv va21 10
			setv va24 plne
			addv va24 100
			inst
			rtar 2 3 24404
			doif targ = null
				new: simp 2 3 24404 "puff-plant" 2 50 va24
				seta va50 targ
			else
				seta va50 null
			endi
			doif tmvt va20 va21 = 0
				kill ownr
			endi
			doif va50 <> null
				kill va50
			endi
*			loop
			inst
			reps 100
				new: simp 2 3 24404 "puff-plant" 2 50 va24
				attr 211

				perm 64
				elas 1
				accg 0.2
				aero 10
				fric 95
				tick 4
*				rnge rand 400 500
				mvto va20 va21
*			Explode in a circle
*				Random direction
				setv va25 rand 0 360
*				Translate to X and Y
				setv va26 sin_ va25
				setv va27 cos_ va25
*				Two "rings" of velocity. So the circle can be full (maybe refactor out the "rand" at some point)
				doif rand 0 1 = 0
					mulv va26 8
					mulv va27 8
				else
					mulv va26 4
					mulv va27 4
				endi
*				Make the "rings" fuzzy. So they turn into a circle
				addv va26 rand -2 2
				addv va27 rand -4 0
				velo va26 va27
			repe
*				wait 20
*			ever										
			wait 3
			targ ownr
			part ov11
			pose 5
			wait rand 0 1000
			kill ownr
		endi
	else
*	state "has flower"

	endi
endm

rscr
enum 2 3 24404
	kill targ
next
enum 2 4 24404
	kill targ
next

scrx 2 3 24404 9
scrx 2 4 24404 9

endm